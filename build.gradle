plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.sonarqube' version '4.4.1.3373'
    id 'io.freefair.lombok' version '8.6'
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group = 'io.github.slimefunguguproject'
version = 'UNOFFICIAL'
description = 'Bump'

java {
    sourceCompatibility = JavaVersion.VERSION_16
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://jitpack.io')
    }

    maven {
        url = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
    }
}

dependencies {
    implementation 'net.guizhanss:GuizhanLib-api:1.7.6'
    implementation 'dev.sefiraat:SefiLib:0.2.6'
    implementation 'org.bstats:bstats-bukkit:3.0.2'

    compileOnly 'org.spigotmc:spigot-api:1.20.4-R0.1-SNAPSHOT'
    compileOnly 'io.github.Slimefun:Slimefun4:RC-37'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

shadowJar {
    relocate 'net.guizhanss.guizhanlib', 'io.github.slimefunguguproject.bump.libs.guizhanlib'
    relocate 'dev.sefiraat.sefilib', 'io.github.slimefunguguproject.bump.libs.sefilib'
    relocate 'org.bstats', 'io.github.slimefunguguproject.bump.libs.bstats'
    minimize()
}

bukkit {
    main = 'io.github.slimefunguguproject.bump.implementation.Bump'
    apiVersion = '1.16'
    authors = ['bxx2004', 'LobbyTech-MC', 'zimzaza4', 'haiman233', 'ybw0014']
    depend = ['Slimefun']
    softDepend = ['GuizhanLibPlugin']
}

sonar {
    properties {
        property "sonar.projectKey", "SlimefunGuguProject_Bump"
        property "sonar.organization", "slimefunguguproject"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
