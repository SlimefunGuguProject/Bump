plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'org.sonarqube' version '4.2.1.3168'
}

group = 'io.github.slimefunguguproject'
description = 'Bump'
java.sourceCompatibility = JavaVersion.VERSION_16

repositories {
    mavenCentral()
    maven {
        url = uri('https://hub.spigotmc.org/nexus/content/repositories/snapshots/')
    }

    maven {
        url = uri('https://jitpack.io')
    }

    maven {
        url = uri('https://s01.oss.sonatype.org/content/repositories/snapshots/')
    }
}

dependencies {
    implementation 'net.guizhanss:GuizhanLib-api:1.3.1'
    implementation 'dev.sefiraat:SefiLib:0.2.6'
    implementation 'org.bstats:bstats-bukkit:3.0.2'

    compileOnly 'org.spigotmc:spigot-api:1.19.2-R0.1-SNAPSHOT'
    compileOnly 'io.github.Slimefun:Slimefun4:RC-32'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    compileOnly 'org.projectlombok:lombok:1.18.28'

    annotationProcessor 'org.projectlombok:lombok:1.18.28'
}

shadowJar {
    // relocate 'net.guizhanss.guizhanlib', 'io.github.slimefunguguproject.bump.guizhanlib'
    relocate 'dev.sefiraat.sefilib', 'io.github.slimefunguguproject.bump.sefilib'
    relocate 'org.bstats', 'io.github.slimefunguguproject.bump.bstats'
    minimize()
}

processResources {
    expand project.properties
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

sonarqube {
    properties {
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.organization', 'slimefunguguproject'
        property 'sonar.projectKey', 'SlimefunGuguProject_Bump'
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
